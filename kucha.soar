#начало
sp {init-robot-propose
    (state <s> ^superstate nil  #проверка, что это первое состояние (нет состояния родителя)
              -^name)           #нет имени
-->
    (<s> ^operator <o> +)       #добавление оператора
    (<o> ^name init-system)     #с именем инициализации
}

#инициализация с target=no
sp {init-robot
    (state <s> ^operator <o>)   #проверка, что есть оператор
    (<o> ^name init-system)     #с именем инициализации
    (<s> ^io <io>)              #у состояния есть сист вход/выход (всегда есть)
    (<io> ^output-link <ol>)    #и выход с названием <ol>
-->
    (<ol> ^target no)           #добавление в <ol> что цели нет
    (<s> ^name start)           #и изменение имени состояния на start (начало работы)
}

#добавление кандидатов на цели
sp {movement-candidate
    (state <s> ^io <io>)        #проверка, что есть сист вход/выход (всегда есть)
    (<io> ^input-link <il>)     #и вход с названием <il>
    (<il> ^away <r>)            #и на входе есть <r>, который далеко
-->
    (<s> ^operator <o> + =)     #добавление оператора (все они равноправны)
    (<o> ^name go-to            #с именем идти-туда
         ^target <r>)           #и целью = <r>
}

#все рядом, никуда идти не надо, танец с бубном
sp {wait
    (state <s> ^io <io>)
    (<io> ^input-link <il>)
    (<il> -^away)
    (<io> ^output-link <ol>)
    (<ol> ^target {<st> <> stay1})
    (<s> ^name <start>)
-->
    (<s> ^operator <o> < +)
    (<o> ^name stay1)
}

sp {wait2
    (state <s> ^io <io>)
    (<io> ^input-link <il>)
    (<il> -^away)
    (<s> ^name <start>)
    (<io> ^output-link <ol>)
    (<ol> ^target stay1)
-->
    (<s> ^operator <o> < +)
    (<o> ^name stay2)
}

sp {stay
    (state <s> ^operator <o>)
    (<o> ^name stay1)
    (<s> ^io <io>)
    (<io> ^output-link <ol>)
    (<ol> ^target <old-target>)
-->
    (<ol> ^target stay1)
    (<ol> ^target <old-target> -)
}

sp {stay2
    (state <s> ^operator <o>)
    (<o> ^name stay2)
    (<s> ^io <io>)
    (<io> ^output-link <ol>)
    (<ol> ^target <old-target>)
-->
    (<ol> ^target stay2)
    (<ol> ^target <old-target> -)
}

#определение цели               
sp {move
    (state <s> ^operator <o>    #проверка, что есть оператор
               ^io <io>)        #и сист вход/выход (всегда есть)
    (<o> ^name go-to            #оператор с именем идти-туда
         ^target <target>)      #и целью <target>
    (<io> ^output-link <ol>)    #и выход с названием <ol>
    (<ol> ^target {<old-target> <> <target>})
-->                             #и эта цель не равна старой
    (<ol> ^target <target>)     #пишем новую цель
    (<ol> ^target <old-target> -) #удаляем старую
}
#как работает если цель не изменяется?????
